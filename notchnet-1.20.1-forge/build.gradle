buildscript {
    repositories {
        maven { url "https://maven.minecraftforge.net" }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
        classpath "com.modrinth.minotaur:Minotaur:2.8.7"
        classpath "net.darkhax.curseforgegradle:CurseForgeGradle:1.1.25"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.modrinth.minotaur"
apply plugin: "net.darkhax.curseforgegradle"

archivesBaseName = "notchnet"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.20.1'
    
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            mods {
                notchnet {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.2.0'
    implementation project(':common')
}

processResources {
    inputs.property "version", project.version

    filesMatching('META-INF/mods.toml') {
        expand 'version': project.version
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = System.getenv("MODRINTH_PROJECT_ID")
    versionNumber = project.version
    versionName = "NotchNet ${project.version} for 1.20.1"
    uploadFile = jar
    gameVersions = ["1.20.1"]
    loaders = ["forge"]
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    def mainFile = upload(System.getenv("CURSEFORGE_PROJECT_ID") ?: 0, jar)
    mainFile.releaseType = "release"
    mainFile.changelog = "See GitHub for changelog"
    mainFile.addGameVersion("1.20.1")
    mainFile.addModLoader("Forge")
}
