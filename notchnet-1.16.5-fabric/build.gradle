plugins {
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.8.7"
	id "net.darkhax.curseforgegradle" version "1.1.25"
}

base {
	archivesName = "notchnet"
}

repositories {
    mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    
    implementation project(':common')
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = System.getenv("MODRINTH_PROJECT_ID")
	versionNumber = project.version
	versionName = "NotchNet ${project.version} for 1.16.5 (Fabric)"
	uploadFile = remapJar
	gameVersions = ["1.16.5"]
	loaders = ["fabric"]
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
	apiToken = System.getenv("CURSEFORGE_TOKEN")
	def mainFile = upload(System.getenv("CURSEFORGE_PROJECT_ID") ?: 0, remapJar)
	mainFile.releaseType = "release"
	mainFile.changelog = "See GitHub for changelog"
	mainFile.addGameVersion("1.16.5")
	mainFile.addModLoader("Fabric")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 8
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}
